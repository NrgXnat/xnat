/*
 * web: build.gradle
 * XNAT https://www.xnat.org
 * Copyright (c) 2005-2020, Washington University School of Medicine and Howard Hughes Medical Institute
 * All Rights Reserved
 *  
 * Released under the Simplified BSD.
 */

buildscript {
    ext {
        vXnat = "1.8.11-SNAPSHOT"
    }
}

plugins {
    id "eclipse"
    id "groovy"
    id "idea"
    id "jacoco"
    id "java-library"
    id "maven-publish"
    id "war"
    id "com.bmuschko.cargo" version "2.8.0"
    id "com.bmuschko.tomcat" version "2.5"
    id "com.dorongold.task-tree" version "1.5"
    id "com.palantir.git-version" version "0.12.1"
    id "io.franzbecker.gradle-lombok" version "4.0.0"
    id "io.spring.dependency-management" version "1.0.11.RELEASE"
    id "net.linguica.maven-settings" version "0.5"
}

group "org.nrg.xnat"
version vXnat
description "XNAT is an open-source imaging informatics software platform dedicated to helping you perform imaging-based research. XNATâ€™s core functions manage importing, archiving, processing and securely distributing imaging and related study data. But its extended uses continue to evolve."

repositories {
    mavenLocal()
    maven { url "https://nrgxnat.jfrog.io/nrgxnat/libs-release" }
    maven { url "https://nrgxnat.jfrog.io/nrgxnat/libs-snapshot" }
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

war.archiveBaseName.set hasProperty("archiveName") ? (archiveName.endsWith(".war") ? archiveName : "${archiveName}.war") : "xnat-web"


configurations {
    javadocCoverage

    all {
        exclude group: "ant", module: "ant"
        exclude group: "avalon-logkit"
        exclude group: "berkeleydb"
        exclude group: "com.github.jai-imageio"
        exclude group: "com.google.code.findbugs", module: "annotations"
        exclude group: "com.metaparadigm"
        exclude group: "com.sun.jdmk", module: "jmxtools"
        exclude group: "com.sun.jmx", module: "jmxri"
        exclude group: "commons-beanutils", module: "commons-beanutils-core"
        exclude group: "commons-betwixt"
        exclude group: "commons-dbcp"
        exclude group: "commons-email"
        exclude group: "commons-logging"
        exclude group: "commons-messenger"
        exclude group: "commons-pool"
        exclude group: "edu.ucar", module: "netcdf"
        exclude group: "excalibur-component"
        exclude group: "excalibur-instrument"
        exclude group: "excalibur-logger"
        exclude group: "excalibur-pool"
        exclude group: "fulcrum"
        exclude group: "geronimo-spec"
        exclude group: "hsqldb"
        exclude group: "imagej"
        exclude group: "jakarta-regexp"
        exclude group: "jamon"
        exclude group: "javax.jms", module: "jms"
        exclude group: "javax.mail", module: "mail"
        exclude group: "javax.servlet", module: "servlet-api"
        exclude group: "javax.sql", module: "jdbc-stdext"
        exclude group: "javax.transaction", module: "jta"
        exclude group: "javax.xml", module: "jsr173"
        exclude group: "jdbc", module: "jdbc"
        exclude group: "jmock"
        exclude group: "jms", module: "jms"
        exclude group: "jndi"
        exclude group: "jython"
        exclude group: "log4j", module: "log4j"
        exclude group: "mockobjects"
        exclude group: "mysql"
        exclude group: "net.sf.saxon"
        exclude group: "ojb", module: "ojb"
        exclude group: "org.apache.geronimo.specs"
        exclude group: "org.apache.struts"
        exclude group: "org.nrg", module: "nrg"
        exclude group: "org.nrg", module: "nrgutil"
        exclude group: "org.nrg", module: "plexiviewer"
        exclude group: "org.nrg.xdat", module: "beans"
        exclude group: "org.nrg.xnat", module: "web"
        exclude group: "org.slf4j", module: "slf4j-log4j12"
        exclude group: "quartz"
        exclude group: "resources", module: "resources"
        exclude group: "servletapi"
        exclude group: "stax", module: "stax-api"
        exclude group: "tomcat"
        exclude group: "velocity"
        exclude group: "xalan"
        exclude group: "xerces"
        exclude group: "xml-apis"
        exclude group: "xml-resolver"
        exclude group: "xmlrpc"
        exclude module: "log4j-slf4j-impl"
        exclude module: "pipelineCNDAXNAT"
        exclude module: "slf4j-simple"
    }
}

configurations.all {
    resolutionStrategy.force "org.mockito:mockito-core:${dependencyManagement.importedProperties["mockito.version"] as String}"
}

dependencies {
    annotationProcessor project(':framework')
    annotationProcessor libs.com.google.auto.value.auto.value
    annotationProcessor libs.com.google.auto.value.auto.value.annotations
    implementation libs.com.google.auto.value.auto.value.annotations

    implementation libs.javax.validation.validation.api
    annotationProcessor libs.javax.validation.validation.api

    annotationProcessor libs.org.projectlombok.lombok
    testAnnotationProcessor libs.org.projectlombok.lombok

    // Each project has a dependency on the platform
    api(platform(project(":parent")))

    // NRG dependencies first.
    api libs.org.nrg.xdat.core
    api libs.org.nrg.xnat.xnat.data.models
    api project(':framework')
    api libs.org.springframework.security.spring.security.ldap
    api libs.org.springframework.security.oauth.spring.security.oauth2
    api libs.org.springframework.security.spring.security.jwt
    api libs.org.springframework.spring.web
    api libs.org.springframework.spring.jms
    api libs.org.springframework.spring.jdbc
    api libs.org.springframework.spring.beans
    api libs.org.springframework.spring.context
    api libs.org.springframework.spring.core

    implementation libs.org.nrg.xnat.spawner
    implementation libs.org.nrg.xdat.core
    implementation project(':framework')
    implementation libs.org.nrg.transaction
    implementation project(':prefs')
    implementation project(':config')
    implementation project(':automation')
    implementation libs.org.nrg.dicomtools
    implementation libs.org.nrg.dicom.dicom.edit4
    implementation libs.org.nrg.dicom.dicom.edit6
    implementation libs.org.nrg.mail
    implementation libs.org.nrg.notify

    implementation libs.org.nrg.dicom.xnat.mx
    implementation libs.org.nrg.dicom.xnat.sop
    implementation libs.org.nrg.dicom.xnat.util
    implementation libs.org.nrg.ecat4xnat
    implementation libs.org.nrg.extattr
    implementation libs.org.nrg.dicomimageutils
    implementation libs.org.nrg.prearcimporter
    implementation libs.org.nrg.sessionbuilders

    implementation(libs.org.nrg.xnat.plexiviewer) {
        transitive = false
    }

    implementation libs.org.nrg.pipelinebase
    implementation libs.org.nrg.pipelinebuild
    implementation libs.org.nrg.pipelineworkflow
    implementation libs.org.nrg.pipelinexnat
    implementation libs.org.nrg.xnat.pipeline.pipeline.client
    implementation libs.org.nrg.xnat.pipeline.xnat.pipeline
    implementation libs.org.nrg.xnat.pipeline.xnat.tools

    implementation libs.dcm4che.dcm4che.core
    implementation libs.dcm4che.dcm4che.image
    implementation libs.dcm4che.dcm4che.imageio
    implementation libs.dcm4che.dcm4che.imageio.rle
    implementation libs.dcm4che.dcm4che.iod
    implementation libs.dcm4che.dcm4che.net
    implementation libs.net.imagej.ij
    implementation libs.org.dcm4che.dcm4che.core
    implementation libs.org.dcm4che.dcm4che.image
    implementation libs.org.dcm4che.dcm4che.imageio

    implementation libs.org.springframework.spring.aop
    implementation libs.org.springframework.spring.context.support
    implementation libs.org.springframework.spring.jms
    implementation libs.org.springframework.spring.messaging
    implementation libs.org.springframework.spring.orm
    implementation libs.org.springframework.spring.oxm
    implementation libs.org.springframework.spring.tx
    implementation libs.org.springframework.spring.webmvc

    implementation libs.io.springfox.springfox.swagger2
    implementation libs.io.springfox.springfox.swagger.ui

    implementation libs.org.springframework.security.spring.security.acl
    implementation libs.org.springframework.security.spring.security.aspects
    implementation libs.org.springframework.security.spring.security.config
    implementation libs.org.springframework.security.spring.security.taglibs
    implementation libs.org.springframework.ldap.spring.ldap.core

    implementation libs.org.hibernate.hibernate.core
    implementation libs.org.hibernate.hibernate.ehcache
    implementation libs.org.hibernate.hibernate.validator
    implementation libs.org.hibernate.hibernate.envers
    implementation libs.net.sf.ehcache.ehcache.core
    implementation libs.com.vladmihalcea.hibernate.types.v43

    implementation libs.org.aspectj.aspectjweaver
    implementation libs.org.aspectj.aspectjrt

    implementation libs.com.noelios.restlet.com.noelios.restlet
    implementation libs.com.noelios.restlet.com.noelios.restlet.ext.servlet
    implementation libs.org.restlet.org.restlet.ext.fileupload

    implementation libs.com.fasterxml.jackson.core.jackson.annotations
    implementation libs.com.fasterxml.jackson.core.jackson.core
    implementation libs.com.fasterxml.jackson.core.jackson.databind
    implementation libs.com.fasterxml.jackson.dataformat.jackson.dataformat.yaml
    implementation libs.com.fasterxml.jackson.datatype.jackson.datatype.hibernate4
    implementation libs.com.fasterxml.jackson.datatype.jackson.datatype.guava
    implementation libs.com.fasterxml.jackson.module.jackson.module.parameter.names
    implementation libs.com.fasterxml.jackson.datatype.jackson.datatype.jdk8
    implementation libs.com.fasterxml.jackson.datatype.jackson.datatype.jsr310
    implementation libs.com.jayway.jsonpath.json.path
    implementation libs.org.json.json

    implementation libs.commons.beanutils.commons.beanutils
    implementation libs.commons.codec.commons.codec
    implementation libs.commons.collections.commons.collections
    implementation libs.commons.configuration.commons.configuration
    implementation libs.commons.digester.commons.digester
    implementation libs.commons.discovery.commons.discovery
    implementation libs.commons.fileupload.commons.fileupload
    implementation libs.commons.net.commons.net

    implementation libs.org.apache.commons.commons.configuration2
    implementation libs.org.apache.commons.commons.math
    implementation libs.org.apache.commons.commons.text
    implementation libs.org.apache.httpcomponents.httpclient
    implementation libs.org.apache.httpcomponents.httpcore
    implementation libs.org.apache.httpcomponents.httpcore.nio
    implementation libs.org.apache.httpcomponents.httpmime
    implementation libs.org.apache.commons.commons.pool2

    implementation libs.org.codehaus.groovy.groovy.all

    implementation libs.eu.bitwalker.useragentutils
    implementation libs.com.twmacinta.fast.md5
    implementation libs.com.h2database.h2
    implementation libs.com.lowagie.itext
    implementation libs.net.java.dev.msv.msv.core
    implementation libs.gnu.getopt.java.getopt
    implementation libs.org.quartz.scheduler.quartz
    implementation libs.org.reflections.reflections
    implementation libs.org.apache.xmlbeans.xmlbeans
    implementation libs.org.apache.xmlbeans.xmlbeans.xpath
    implementation libs.org.apache.xbean.xbean.spring
    implementation libs.net.java.dev.msv.xsdlib
    implementation libs.javax.servlet.jstl
    implementation libs.com.google.code.gson.gson
    implementation libs.com.google.guava.guava
    implementation libs.org.postgresql.postgresql
    implementation libs.net.ttddyy.datasource.proxy
    implementation libs.org.apache.activemq.activemq.broker
    implementation libs.org.apache.activemq.activemq.spring
    implementation libs.org.apache.activemq.activemq.kahadb.store
    implementation libs.org.apache.activemq.activemq.jdbc.store
    implementation libs.io.micrometer.micrometer.core
    implementation libs.io.micrometer.micrometer.registry.prometheus

    implementation libs.org.slf4j.slf4j.api
    implementation libs.org.slf4j.jul.to.slf4j
    implementation libs.org.slf4j.jcl.over.slf4j
    implementation libs.org.slf4j.log4j.over.slf4j
    implementation libs.ch.qos.logback.logback.classic
    implementation libs.ch.qos.logback.logback.core
    implementation libs.net.logstash.logback.logstash.logback.encoder

    providedCompile libs.com.google.code.findbugs.jsr305
    providedCompile libs.javax.servlet.javax.servlet.api
    providedCompile libs.org.jetbrains.annotations
    providedCompile libs.org.kohsuke.metainf.services.metainf.services

    runtimeOnly libs.org.apache.ant.ant
    runtimeOnly libs.axis.axis.ant
    runtimeOnly libs.axis.axis.schema
    runtimeOnly libs.cglib.cglib
    runtimeOnly libs.com.zaxxer.hikaricp
    runtimeOnly libs.org.hsqldb.hsqldb
    runtimeOnly libs.com.sun.media.jai.imageio
    runtimeOnly libs.net.bull.javamelody.javamelody.core
    runtimeOnly libs.org.javassist.javassist
    runtimeOnly libs.org.python.jython.standalone

    tomcat libs.org.apache.tomcat.embed.tomcat.embed.core,
            libs.org.apache.tomcat.embed.tomcat.embed.logging.juli,
            libs.org.apache.tomcat.embed.tomcat.embed.jasper

    cargo libs.org.codehaus.cargo.cargo.core.uberjar,
            libs.org.codehaus.cargo.cargo.ant,
            libs.org.slf4j.jcl.over.slf4j,
            libs.org.slf4j.slf4j.api,
            libs.ch.qos.logback.logback.classic,
            libs.ch.qos.logback.logback.core

    testImplementation libs.org.nrg.test
    testImplementation libs.junit.junit
    testImplementation libs.org.springframework.spring.test
    testImplementation libs.org.assertj.assertj.core
    testImplementation libs.org.springframework.security.spring.security.test
    testImplementation libs.org.springframework.security.spring.security.config
    testImplementation libs.org.hamcrest.hamcrest
    testImplementation libs.org.mockito.mockito.core
    testImplementation libs.org.powermock.powermock.api.mockito2
    testImplementation libs.org.powermock.powermock.module.junit4

    javadocCoverage libs.com.manoelcampos.javadoc.coverage
}

idea {
    module {
        inheritOutputDirs = false
        outputDir = compileJava.destinationDir
        testOutputDir = compileTestJava.destinationDir
    }
}

tomcatRun.contextPath = "/"
tomcatRunWar.contextPath = "/"

[tomcatRun, tomcatRunWar].each { task ->
    task.additionalRuntimeResources << file("${System.getenv().XNAT_HOME ?: System.getenv().HOME}/config/plugins")
}

["Java", "War"].each { type ->
    ["Local", "Repository"].each { target ->
        def artifactName = type == "Java" ? "Jar" : type
        def taskName = "publish${artifactName}To${target}"
        logger.info "Creating task ${taskName}"
        tasks.create(name: taskName) {
            description = "Publishes the Maven ${type} publications to Maven ${target}."
            dependsOn tasks.withType(AbstractPublishToMaven).matching {
                it.name == "publishMaven${type}PublicationToMaven${target}"
            }
        }
    }
}

task deployToTomcat(type: Copy, dependsOn: war) {
    from war.archiveFile
    into "${propertyWithDefault("tomcatHome", ".")}/webapps"
}

cargo {
    // These properties must be set in the ~/.gradle/gradle.properties file or passed on the Gradle command line in
    // the form -PdeployPort=8080 -PdeployContext=/ -PdeployHost=localhost -PdeployUser=deploy -PdeployPassword=deploy
    containerId = "tomcat8x"
    port = propertyWithDefault("deployPort", 8080) as int

    deployable {
        context = propertyWithDefault("deployContext", "/")
    }

    remote {
        hostname = propertyWithDefault("deployHost", "localhost")
        username = propertyWithDefault("deployUser", "deploy")
        password = propertyWithDefault("deployPassword", "deploy")
    }
}

// Pulls in the Jenkins BUILD_NUMBER environment variable if available.
def buildDate = new Date()
def buildNumber = System.getenv().BUILD_NUMBER?.toInteger() ?: "Manual"
def isDirty, branchName, gitHash, gitHashFull, commitDistance, lastTag, isCleanTag

try {
    def gitDetails = versionDetails()
    isDirty = gitVersion().endsWith ".dirty"
    branchName = gitDetails.branchName ?: "Unknown"
    gitHash = gitDetails.gitHash
    gitHashFull = gitDetails.gitHashFull
    commitDistance = gitDetails.commitDistance
    lastTag = gitDetails.lastTag
    isCleanTag = gitDetails.isCleanTag
} catch (IllegalArgumentException e) {
    logger.info "Got an error trying to read VCS metadata from git. It's possible this project is not under VCS control. Using placeholder values for manifest entries."
    isDirty = true
    branchName = "Unknown"
    gitHash = "None"
    gitHashFull = "None"
    commitDistance = 0
    lastTag = "None"
    isCleanTag = false
}

logger.info "Build-Date:              ${buildDate}"
logger.info "Build-Number:            ${buildNumber}"
logger.info "Implementation-Version:  ${version}"
logger.info "Implementation-Sha-Full: ${gitHashFull}"
logger.info "Implementation-Sha:      ${gitHash}"
logger.info "Implementation-Commit:   ${commitDistance}"
logger.info "Implementation-LastTag:  ${lastTag}"
logger.info "Implementation-Branch:   ${branchName}"
logger.info "Implementation-CleanTag: ${isCleanTag}"
logger.info "Implementation-Dirty:    ${isDirty}"

ext.gitManifest = manifest {
    attributes "Application-Name": "XNAT",
            "Build-Date": buildDate,
            "Build-Number": buildNumber,
            "Implementation-Version": project.version,
            "Implementation-Sha": gitHash,
            "Implementation-Sha-Full": gitHashFull,
            "Implementation-Commit": commitDistance,
            "Implementation-LastTag": lastTag,
            "Implementation-Branch": branchName,
            "Implementation-CleanTag": isCleanTag,
            "Implementation-Dirty": isDirty
}

jar {
    manifest {
        from gitManifest
    }
}

war {
    duplicatesStrategy = DuplicatesStrategy.WARN
    manifest {
        from gitManifest
    }
}

test {
    systemProperties["xnat.home"] = "${buildDir}/tmp/test"
}

jacoco {
    toolVersion = libs.versions.jacoco.get()
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = false
        csv.required = false
        html.required = true
    }
}

task javadocCoverageReport(type: Javadoc, dependsOn: javadoc) {
    source = sourceSets.main.allJava
    destinationDir = reporting.file("javadoc")
    classpath = configurations.compileClasspath
    options.docletpath = configurations.javadocCoverage.files.asType(List)
    options.doclet = "com.manoelcampos.javadoc.coverage.CoverageDoclet"
}

task sourceJar(type: Jar, dependsOn: classes) {
    archiveClassifier = "sources"
    manifest {
        from gitManifest
    }
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    archiveClassifier = "javadoc"
    manifest {
        from gitManifest
    }
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifacts {
                artifact sourceJar
                artifact javadocJar
            }

            pom.withXml {
                def root = asNode()
                populatePom(root)
            }
        }
        mavenWar(MavenPublication) {
            groupId = "${project.group}.${rootProject.name}"
            artifactId = "xnat-${rootProject.name}"

            from components.web

            pom.withXml {
                def root = asNode()
                populatePom(root)
            }
        }
    }
    repositories {
        maven {
            url project.version.endsWith("-SNAPSHOT") ? "https://nrgxnat.jfrog.io/nrgxnat/libs-snapshot-local" : "https://nrgxnat.jfrog.io/nrgxnat/libs-release-local"
            // The value for name must match <id> in ~/.m2/settings.xml
            name = "XNAT_Artifactory"
        }
    }
}

def propertyWithDefault(String name, Object value) {
    hasProperty(name) ? property(name) : value
}

def populatePom(def root) {
    root.appendNode("name", "XNAT Server")
    root.appendNode("description", project.description)
    root.appendNode("url", "https://bitbucket.org/xnatdev/xnat-web")
    root.appendNode("inceptionYear", "2019")

    def scm = root.appendNode("scm")
    scm.appendNode("url", "https://bitbucket.org/xnatdev/xnat-web")
    scm.appendNode("connection", "scm:https://bitbucket.org/xnatdev/xnat-web.git")
    scm.appendNode("developerConnection", "scm:git://bitbucket.org/xnatdev/xnat-web.git")

    def license = root.appendNode("licenses").appendNode("license")
    license.appendNode("name", "Simplified BSD 2-Clause License")
    license.appendNode("url", "https://xnat.org/about/license.php")
    license.appendNode("distribution", "repo")

    def developers = root.appendNode("developers")
    [["akaushal", "Atul Kaushal", "atul.hkaushal@gmail.com"],
     ["cmoore", "Charlie Moore", "moore.c@wustl.edu"],
     ["dmaffitt", "Dave Maffitt", "david.maffitt@wustl.edu"],
     ["jcleveland", "Justin Cleveland", "clevelandj@wustl.edu"],
     ["jflavin", "John Flavin", "flavinj@gmail.com"],
     ["jgurney", "Jenny Gurney", "gkgurney@wustl.edu"],
     ["jransford", "James Ransford", "ransfordj@radiologics.com"],
     ["kalpert", "Kate Alpert", "kate@radiologics.com"],
     ["mflorida", "Mark Florida", "markflorida@wustl.edu"],
     ["mhodge", "Mike Hodge", "hodgem@wustl.edu"],
     ["mkelsey", "Matt Kelsey", "kelseym@mir.wustl.edu"],
     ["mmcKay", "Mike McKay", "mfmckay@wustl.edu"],
     ["mramaratnam", "Mohana Ramaratnam", "mohana@radiologics.com"],
     ["rherrick", "Rick Herrick", "jrherrick@wustl.edu"],
     ["smoore", "Steve Moore", "moores@mir.wustl.edu"],
     ["tolsen", "Tim Olsen", "tim@radiologics.com"],
     ["whorton", "Will Horton", "hortonw@wustl.edu"]].forEach({ info ->
        def developer = developers.appendNode("developer")
        developer.appendNode("id", info[0])
        developer.appendNode("name", info[1])
        developer.appendNode("email", info[2])
    })
}
